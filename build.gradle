plugins {
    id 'java'
    id 'idea'
}

group 'org.iota'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompileOnly, testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compileOnly ('com.github.iotaledger:ict:0.7-SNAPSHOT')
    runtimeClasspath ('com.github.iotaledger:ict:0.6')
    testCompileOnly ('com.github.iotaledger:ict:0.6')
    compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtimeClasspath group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.21.0'
}

task ixi(type: Jar) {
    baseName = 'serialization'
    destinationDir = file("$rootDir")
    from("./") { include 'module.json' }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test